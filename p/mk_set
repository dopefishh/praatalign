#!/bin/csh 

# creates the language indepenedent SAM-PA MAUS set

# CAVEAT: Only run this script on your installation, if you 
#         exactly know, what you are doing!

# Source are the three manually maintained files that define 
# the SAMPA set and the mapping to HMM:
# SAMPA.inv : base symbols
# SAMPA.dia : extensions
# SAMPA.map : mapping of base names/extensions that do not have 
#             a trained HMM in SUPERHMM to existing HMMs in SUPERHMM
#             (note: HMM names have 'P' marker before numerals!)
#             and the SUPERHMM.lst|mmf set of trained HMMs for SAMPA 
#             symbols derived from the different language sets 
# (Note that in the latter SAMPA symbols are extended by a Iso639-3 code!)

# We combine the 1st columns of SAMPA.inv|dia sorted by descending 
# string length into KANINVENTAR, and the complete tables SAMPA.inv|dia
# into KANINVENTAR.inv.
# Symbols with trailing '\' or ''' cause an error, because
# backslashes and high comma cannot be processed by MAUS.
# We copy KANINVENTAR to GRAPHINVENTAR and replace nummerical
# SAM-PA symbols by preceeding 'P' (e.g. '2:' > 'P2:').
# For the mapping (DICT) from symbol (GRAPHINVENTAR) to MMF.mmf (HMMINVENTAR) we start with 
# the symbols that can be found in SUPERHMM.lst since they have a 
# one-to-one mapping to a HMM; note that SAMPA symbols in SUPERHMM.lst
# have Iso649-3 language code attached; the first occurance of a matching SAMPA symbol
# is selected for mapping and the information from which language set this
# HMM is derived is written into the 7th column (HMM) of KANINVENTAR.inv. 
# Then we look at the remaining symbols and check 
# SAMPA.map for a mapping to existing HMMs in SUPERHMM.mmf; if a symbol
# cannot be found in SAMPA.map, we through a warning and print UNKNOWN in 
# the 2nd column of DICT.
# Finally the 2nd column of DICT is copied to HMMINVENTAR.

# If you miss a SAM-PA symbol in the SAMPA MAUS inventar: 
# Define it properly in SAMPA.inv or .dia.
# Check the list SUPERHMM.lst for a HMM that might be a good
# acoustic approximation of the symbol. Then add an entry to SAMPA.map
# that maps the new symbol to the existing HMM and run this script

# make list of required SAM-PA symbols sorted descending by string length
cut -f 1 SAMPA.inv SAMPA.dia | grep -v '^MAUS' | gawk '{printf("%d\t%s\n",length($1),$1)}' | \
  sort -rn | cut -f 2 >! KANINVENTAR
cat SAMPA.inv SAMPA.dia >! KANINVENTAR.inv

# make internal symbol set (= KANINVENTAR with P-masked numerical symbols)
sed 's/\([1-9]\)/P\1/g' KANINVENTAR >! GRAPHINVENTAR

# make mapping (DICT) from symbol (GRAPHINVENTAR) to HMM (HMMINVENTAR)
if ( -e DICT ) rm -f DICT
touch DICT
foreach s ( "`cat GRAPHINVENTAR`" )
  # remove P marker
  # the following idiotic construction is just because of the SAMPA /{/
  echo "${s}" | sed 's/P\([0-9]\)/\1/g' >! /tmp/$$s_p
  set s_p = "`cat /tmp/$$s_p`"
  rm -f /tmp/$$s_p
  grep -q "^${s}"'\....$' SUPERHMM.lst
  if ( $status == 0 ) then 
    # symbol has a HMM in SUPERHMM -> simple mapping to the first occurance in SUPERHMM
    # the following idiotic construction is just because the SAMPA /{/
    grep "^${s}"'\....$' SUPERHMM.lst | head -n 1 >! /tmp/$$s_lang
    set s_lang = "`cat /tmp/$$s_lang`"
    rm -f /tmp/$$s_lang
    # add mapping entry
    printf "%s %s\n" "$s" "$s_lang" >> DICT
    # add 7th column entry into KANINVENTAR.inv
    sed "s/^\(${s_p}	"'.*$\)/\1'"	${s_lang}/" KANINVENTAR.inv >! /tmp/KANINVENTAR.inv
    mv /tmp/KANINVENTAR.inv KANINVENTAR.inv
  else
    # no HMM yet, use mapping from SAMPA.map
    grep "^${s_p}	" SAMPA.map | cut -f 2 >! /tmp/$$m_s
    if ( $status != 0 ) then 
      echo "WARNING: SAMPA symbol ${s_p} has no mapping in SAMPA.map"
      echo "${s} UNKNOWN" >> DICT
    else 
      # check for mapped symbol in SUPERHMM
      set m_s = "`cat /tmp/$$m_s`"
      rm -f /tmp/$$m_s
      grep -q "^${m_s}"'$' SUPERHMM.lst
      if ( $status != 0 ) then 
        echo "ERROR: cannot find mapping target $m_s in SUPERHMM - exiting"
	exit -1
      endif
      # make entry into DICT
      echo "${s} ${m_s}" >> DICT
      # make 7th column entry in KANINVENTAR.inv
      sed "s/^\(${s_p}	"'.*$\)/\1'"	${m_s}/" KANINVENTAR.inv >! /tmp/KANINVENTAR.inv
      mv /tmp/KANINVENTAR.inv KANINVENTAR.inv
    endif
  endif  
end

# make HMM inventar list for HTK
cut -d ' ' -f 2 DICT | sort -u >! HMMINVENTAR

exit 0




